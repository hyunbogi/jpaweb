group 'com.hyunbogi'
version '1.0-SNAPSHOT'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE'
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.5"
    }
}

apply plugin: 'io.spring.dependency-management'
apply plugin: "com.ewerk.gradle.plugins.querydsl"
apply plugin: 'war'
apply plugin: 'idea'

ext {
    // Dependency version
    springVersion = '4.2.3.RELEASE'
    springDataVersion = 'Gosling-RELEASE'
    mysqlConnectorVersion = '5.1.37'
    hibernateVersion = '5.0.2.Final'
    slf4jVersion = '1.7.13'
    tomcatJdbcVersion = '8.0.28'
    jspApiVersion = '2.2.1-b03'
    logbackClassicVersion = '1.1.3'
    junitVersion = '4.12'
    servletApiVersion = '3.1.0'
    jstlVersion = '1.2'
    lombokVersion = '1.16.6'
    querydslVersion = '3.6.9'
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework:spring-framework-bom:${springVersion}"
        mavenBom "org.springframework.data:spring-data-releasetrain:${springDataVersion}"
    }
}

dependencies {
    // MySQL Connector
    runtime "mysql:mysql-connector-java:${mysqlConnectorVersion}"

    // DB Connection pool
    compile "org.apache.tomcat:tomcat-jdbc:${tomcatJdbcVersion}"

    // Spring Framework
    compile "org.springframework:spring-webmvc"

    // Spring Data JPA
    compile "org.springframework.data:spring-data-jpa"

    // JPA, Hibernate
    compile "org.hibernate:hibernate-entitymanager:${hibernateVersion}"

    // Logging
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    runtime "ch.qos.logback:logback-classic:${logbackClassicVersion}"

    // QueryDSL
    compile "com.mysema.querydsl:querydsl-jpa:${querydslVersion}"

    // WEB
    providedCompile "javax.servlet:javax.servlet-api:${servletApiVersion}"
    runtime "javax.servlet.jsp:jsp-api:${jspApiVersion}"
    runtime "jstl:jstl:${jstlVersion}"

    // Lombok
    providedCompile "org.projectlombok:lombok:${lombokVersion}"

    // Test
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
}

// For QueryDSL plugin
querydsl {
    library = "com.mysema.querydsl:querydsl-apt:${querydslVersion}"
    querydslSourcesDir = 'src/querydsl/java'
    jpa = true
}

// For IDEA plugin
idea {
    module {
        generatedSourceDirs += file(querydsl.querydslSourcesDir)
    }
}
